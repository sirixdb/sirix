buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.17.12"
    }
}

test {
  useJUnitPlatform()
}

apply plugin: 'kotlin'
apply plugin: 'com.gradleup.shadow'
//apply plugin: 'org.jetbrains.dokka'
apply plugin: 'docker-compose'
apply plugin: 'org.graalvm.buildtools.native'

dependencies {
    implementation project(':sirix-core')
    implementation project(':sirix-query')
    testImplementation project(':sirix-core').sourceSets.test.output

    implementation implLibraries.kotlinStdlib
    implementation implLibraries.kotlinxCoroutinesCore
    implementation implLibraries.vertxCore
    implementation implLibraries.vertxWeb
    implementation implLibraries.vertxLangKotlin
    implementation implLibraries.vertxLangKotlinCoroutines
    implementation implLibraries.vertxAuthOauth2
    implementation implLibraries.vertxWebClient
    implementation implLibraries.vertxConfig
    implementation implLibraries.vertxLauncher
    implementation implLibraries.kotlinStdlibJdk8
    implementation implLibraries.jsoup
    testImplementation testLibraries.kotlinTestJunit
    testImplementation testLibraries.junit
    testImplementation testLibraries.junitJupiterApi
    testImplementation testLibraries.junitJupiterEngine
    testImplementation testLibraries.junitPlatformLauncher
    testImplementation testLibraries.vertxJunit5
    testImplementation testLibraries.jsonassert

}



description = 'Non-blocking REST-API for SirixDB.'

kotlin {
    jvmToolchain(25)
}

shadowJar {
    archiveClassifier = 'fat'
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
    mergeServiceFiles()
}

jar {
	manifest {
		attributes('Main-Verticle': 'io.sirix.rest.SirixVerticle',
				   'Main-Class': 'io.vertx.launcher.application.VertxApplication')
	}
    finalizedBy shadowJar
}

test {
    jvmArgs = [
        "-Duser.home=${buildDir}",
        '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
        '--add-opens', 'java.base/java.nio=ALL-UNNAMED',
        '--add-modules', 'jdk.incubator.vector',
        '--enable-native-access=ALL-UNNAMED'
    ]
}

task copyCertificates(type: Copy) {
    from file("$buildDir/resources/main")
    include '*.pem'
    into file("$buildDir/sirix-data")
}
copyCertificates.dependsOn(processResources)

/*
task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    configuration {
        platform = "JVM"
        jdkVersion = 8
    }
}

javadocJar.dependsOn dokkaJavadoc*/

dockerCompose.isRequiredBy(test)

test.dependsOn copyCertificates
test.dependsOn composeUp
test.finalizedBy composeDownForced

dockerCompose {
    useComposeFiles = ["${projectDir}/src/test/resources/docker-compose.yml"]

   // removeImages = "com.avast.gradle.dockercompose.RemoveImages.None" // Other accepted values are: "All" and "Local"

    composeLogToFile = project.file('sirix-logs.txt') // redirect output of composeUp and composeDown tasks to this file; default is null (ouput is not redirected)
    containerLogToDir = project.file('logs') // directory where composeLogs task stores output of the containers; default: build/containers-logs

    projectName = 'sirix-test'
}

// Task to run only native-image smoke tests on JVM (for validation before native compilation)
// This task does NOT require Docker/Keycloak
tasks.register('nativeImageSmokeTest', Test) {
    description = 'Run native-image smoke tests on JVM (no Docker required)'
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform {
        includeTags 'native-image'
    }
    jvmArgs = [
        '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
        '--add-opens', 'java.base/java.nio=ALL-UNNAMED',
        '--add-modules', 'jdk.incubator.vector',
        '--enable-native-access=ALL-UNNAMED'
    ]
    minHeapSize = '2g'
    maxHeapSize = '4g'
}

// GraalVM Native Image Configuration
graalvmNative {
    registerTestBinary("smokeTest") {
        usingSourceSet(sourceSets.test)
        forTestTask(tasks.named("nativeImageSmokeTest"))
    }
    binaries {
        // Smoke test binary (compiled to native image via nativeSmokeTest task)
        smokeTest {
            buildArgs.add('--enable-native-access=ALL-UNNAMED')
            buildArgs.add('--add-modules=jdk.incubator.vector')
            buildArgs.add('-H:+ForeignAPISupport')
            buildArgs.add('-H:+UnlockExperimentalVMOptions')
            // Logging frameworks - build time
            buildArgs.add('--initialize-at-build-time=org.slf4j')
            buildArgs.add('--initialize-at-build-time=ch.qos.logback')
            buildArgs.add('--initialize-at-build-time=com.fasterxml.jackson')
            // Netty - all run time
            buildArgs.add('--initialize-at-run-time=io.netty')
            // Sirix FFI components - run time
            buildArgs.add('--initialize-at-run-time=io.sirix.cache.LinuxMemorySegmentAllocator')
            buildArgs.add('--initialize-at-run-time=io.sirix.cache.WindowsMemorySegmentAllocator')
            buildArgs.add('--initialize-at-run-time=io.sirix.io.bytepipe.FFILz4Compressor')
            // Vert.x components - run time
            buildArgs.add('--initialize-at-run-time=io.vertx.core.impl.resolver')
            buildArgs.add('--initialize-at-run-time=io.vertx.core.dns')
            // Use G1 GC
            buildArgs.add('--gc=G1')
            // Quick build for faster test compilation
            quickBuild = true
            // Allocate memory to the native-image compiler
            jvmArgs.add('-XX:MaxRAMPercentage=80')
        }
        main {
            imageName = 'sirix-rest-api'
            mainClass = 'io.vertx.launcher.application.VertxApplication'
            // Build executable, not shared library
            sharedLibrary = false
            buildArgs.add('--enable-native-access=ALL-UNNAMED')
            buildArgs.add('--add-modules=jdk.incubator.vector')
            buildArgs.add('-H:+ForeignAPISupport')
            buildArgs.add('-H:+UnlockExperimentalVMOptions')
            buildArgs.add('--no-fallback')
            // Exclude problematic GraalVM library-support substitutions
            buildArgs.add('--exclude-config')
            buildArgs.add('library-support.jar')
            buildArgs.add('META-INF/native-image/.*')
            // Vert.x main verticle
            buildArgs.add('-Dvertx.main=io.sirix.rest.SirixVerticle')
            // Include config files from classpath
            buildArgs.add('-H:IncludeResources=.*\\.properties$')
            buildArgs.add('-H:IncludeResources=.*\\.xml$')
            buildArgs.add('-H:IncludeResources=.*\\.json$')
            buildArgs.add('-H:IncludeResources=.*\\.pem$')
            // Logging frameworks - build time
            buildArgs.add('--initialize-at-build-time=org.slf4j')
            buildArgs.add('--initialize-at-build-time=ch.qos.logback')
            buildArgs.add('--initialize-at-build-time=com.fasterxml.jackson')
            // Netty - all run time to avoid InetAddress in image heap issues
            buildArgs.add('--initialize-at-run-time=io.netty')
            // Sirix FFI components
            buildArgs.add('--initialize-at-run-time=io.sirix.cache.LinuxMemorySegmentAllocator')
            buildArgs.add('--initialize-at-run-time=io.sirix.cache.WindowsMemorySegmentAllocator')
            buildArgs.add('--initialize-at-run-time=io.sirix.io.bytepipe.FFILz4Compressor')
            // Vert.x DNS resolver (contains InetAddress references)
            buildArgs.add('--initialize-at-run-time=io.vertx.core.impl.resolver')
            buildArgs.add('--initialize-at-run-time=io.vertx.core.dns')
            // Use G1 GC for better latency and throughput at runtime
            buildArgs.add('--gc=G1')
            // Set max heap for the native image runtime
            buildArgs.add('-R:MaxHeapSize=4g')

            // Profile-Guided Optimization (PGO) support
            // Step 1: Build instrumented binary: ./gradlew :sirix-rest-api:nativeCompile -Ppgo-instrument
            // Step 2: Run workload to collect profile (generates default.iprof)
            // Step 3: Build optimized binary: ./gradlew :sirix-rest-api:nativeCompile -Ppgo=default.iprof
            if (project.hasProperty('pgo-instrument')) {
                buildArgs.add('--pgo-instrument')
            }
            if (project.hasProperty('pgo')) {
                buildArgs.add("--pgo=${project.findProperty('pgo')}")
            }

            // Enable verbose output for debugging
            verbose = true
            // Quick build uses less memory and is faster (disable for production)
            quickBuild = project.hasProperty('quick-build') ? true : false
            // Allocate more memory to the native-image compiler (use 80% of available RAM)
            jvmArgs.add('-XX:MaxRAMPercentage=80')
        }
    }
    toolchainDetection = false
}
